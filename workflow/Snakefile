# vim: nofoldenable

env = "lstchain-v0.9.13"
config = "config.json"


include: "rules/requirements.smk"


rule all:
    """Docstring comment for a rule."""
    input:
        "build/runs.json",


rule download_runlist:
    input:
        "lst1-authentication.txt",
    output:
        "build/runlist.html",
    conda:
        env
    shell:
        "\
        curl --user $(cat {input}) -s \
            https://lst1.iac.es/datacheck/lstosa/LST_source_catalog.html \
            -o {output} \
        "


rule select_datasets:
    input:
        "build/runlist.html",
        config=config,
    output:
        "build/runlist.csv",
    conda:
        env
    shell:
        "python scripts/select-data.py {input[0]} {output} -c {input.config}"


rule merge_datachecks:
    input:
        "build/runlist.csv",
        config=config,
    output:
        "build/dl1-datachecks-merged.h5",
    conda:
        env
    shell:
        "python scripts/merge-datachecks.py {input[0]} {output} -c {input.config}"


rule run_ids:
    output:
        "build/runs.json",
    input:
        "build/runlist-checked.csv",
        config=config,
    conda:
        env
    shell:
        "python scripts/create-night-run-list.py {input[0]} {output} -c {input.config}"


rule data_check:
    input:
        "build/runlist.csv",
        "build/dl1-datachecks-merged.h5",
        config=config,
    output:
        runlist="build/runlist-checked.csv",
        datachecks="build/dl1-datachecks-masked.h5",
    conda:
        env
    shell:
        "\
        python \
            scripts/data-check.py \
            {input[0]} \
            {input[1]} \
            --output-runlist {output.runlist} \
            --output-datachecks {output.datachecks} \
            --config {input.config} \
        "


rule authentication:
    output:
        "lst1-authentication.txt",
    shell:
        "echo 'Provide the file {output} as explained in README.md'"


rule plot_moon_illumination:
    input:
        "build/dl1-datachecks-masked.h5",
        config=config,
    output:
        "build/moon.pdf",
    conda:
        env
    shell:
        "python scripts/plot-moon-illumination.py {input[0]} -c {input.config} -o {output}"
